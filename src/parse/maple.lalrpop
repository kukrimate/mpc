use crate::parse::*;
use std::str::FromStr;

grammar(module: &mut Module);

Path: Path = {
  Identifier  => vec![<>],
  <mut v:Path> "::" <i:Identifier> => {
    v.push(i);
    v
  }
};

// Types

TypeName: TyRef = {
  "Bool"      => TyRef::Bool,
  "Uint8"     => TyRef::Uint8,
  "Int8"      => TyRef::Int8,
  "Uint16"    => TyRef::Uint16,
  "Int16"     => TyRef::Int16,
  "Uint32"    => TyRef::Uint32,
  "Int32"     => TyRef::Int32,
  "Uint64"    => TyRef::Uint64,
  "Int64"     => TyRef::Int64,
  "Uintn"     => TyRef::Uintn,
  "Intn"      => TyRef::Intn,
  "Float"     => TyRef::Float,
  "Double"    => TyRef::Double,
  <p:Path>    => TyRef::Path(p),
  "*" <is_mut:IsMut> <ty:TypeName> =>
    TyRef::Ptr(is_mut, Box::new(ty)),
  "Function" "(" <params:ParameterListOpt> ")" <ret:ReturnType> =>
    TyRef::Func(params, Box::new(ret)),
  "[" <l:Expr> "]" <t:TypeName> =>
    TyRef::Arr(Box::new(l), Box::new(t)),
  "(" <params:ParameterListOpt>  ")" =>
    TyRef::Tuple(params)
};

ParameterListOpt: Vec<(RefStr, TyRef)> = {
  ParameterList,
  => vec![],
};

ParameterList: Vec<(RefStr, TyRef)> = {
  <name:Identifier> ":" <ty:TypeName> => vec![ (name, ty) ],
  <mut l:ParameterList> "," <name:Identifier> ":" <ty:TypeName> => {
    l.push((name, ty));
    l
  },
};

ReturnType: TyRef = {
  "->" <t:TypeName> => t,
  => TyRef::Tuple(vec![])
};

IsMut: IsMut = {
  "mut" => IsMut::Yes,
        => IsMut::No
};

// Blocks

Block: Expr = {
  "{" "}" => Expr::Null,
  "{" <v:BlockItemList> "}" => Expr::Block(v),
  "{" <mut v:BlockItemList> ";" "}" => {
    v.push(Expr::Null);
    Expr::Block(v)
  }
};

BlockItemList: Vec<Expr> = {
  BlockItem => vec![<>],
  <mut v:BlockItemList> ";" <i:BlockItem> => {
    v.push(i);
    v
  }
};

BlockItem: Expr = {
  Expr,
  "let" <is_mut:IsMut> <name:Identifier> <ty:LetType> "=" <init:Expr> =>
    Expr::Let(name, is_mut, ty, Box::new(init)),
  "continue" => Expr::Continue,
  "break" <e:ExprOpt> => Expr::Break(Box::new(e)),
  "return" <e:ExprOpt> => Expr::Return(Box::new(e)),
  "if" <e:Expr> <b:Block> => Expr::If(Box::new(e), Box::new(b), Box::new(Expr::Null)),
  "while" <e:Expr> <b:Block> => Expr::While(Box::new(e), Box::new(b)),

  <l:Expr> "=" <r:Expr> => Expr::As(Box::new(l), Box::new(r)),
  <l:Expr> "*=" <r:Expr> => Expr::Rmw(BinOp::Mul, Box::new(l), Box::new(r)),
  <l:Expr> "/=" <r:Expr> => Expr::Rmw(BinOp::Div, Box::new(l), Box::new(r)),
  <l:Expr> "%=" <r:Expr> => Expr::Rmw(BinOp::Mod, Box::new(l), Box::new(r)),
  <l:Expr> "+=" <r:Expr> => Expr::Rmw(BinOp::Add, Box::new(l), Box::new(r)),
  <l:Expr> "-=" <r:Expr> => Expr::Rmw(BinOp::Sub, Box::new(l), Box::new(r)),
  <l:Expr> "<<=" <r:Expr> => Expr::Rmw(BinOp::Lsh, Box::new(l), Box::new(r)),
  <l:Expr> ">>=" <r:Expr> => Expr::Rmw(BinOp::Rsh, Box::new(l), Box::new(r)),
  <l:Expr> "&=" <r:Expr> => Expr::Rmw(BinOp::And, Box::new(l), Box::new(r)),
  <l:Expr> "^=" <r:Expr> => Expr::Rmw(BinOp::Xor, Box::new(l), Box::new(r)),
  <l:Expr> "|=" <r:Expr> => Expr::Rmw(BinOp::Or, Box::new(l), Box::new(r)),
};

ExprOpt: Expr = {
  <e:Expr>  => e,
            => Expr::Null
};

LetType: Option<TyRef> = {
  ":" <ty:TypeName> => Some(ty),
                    => None,
};

// Expressions

Expr: Expr = {
  LOrExpr,
  "if" <c:Expr> <t:Block> "else" <e:Block> => Expr::If(Box::new(c), Box::new(t), Box::new(e)),
  "loop" <b:Block> => Expr::Loop(Box::new(b))
};

LOrExpr: Expr = {
  LAndExpr,
  <l:LOrExpr> "||" <r:LAndExpr> => Expr::LOr(Box::new(l), Box::new(r))
};

LAndExpr: Expr = {
  CmpExpr,
  <l:LAndExpr> "&&" <r:CmpExpr> => Expr::LAnd(Box::new(l), Box::new(r))
};

CmpExpr: Expr = {
  OrExpr,
  <l:OrExpr> "==" <r:OrExpr> => Expr::Bin(BinOp::Eq, Box::new(l), Box::new(r)),
  <l:OrExpr> "!=" <r:OrExpr> => Expr::Bin(BinOp::Ne, Box::new(l), Box::new(r)),
  <l:OrExpr> "<" <r:OrExpr> => Expr::Bin(BinOp::Lt, Box::new(l), Box::new(r)),
  <l:OrExpr> ">" <r:OrExpr> => Expr::Bin(BinOp::Gt, Box::new(l), Box::new(r)),
  <l:OrExpr> "<=" <r:OrExpr> => Expr::Bin(BinOp::Le, Box::new(l), Box::new(r)),
  <l:OrExpr> ">=" <r:OrExpr> => Expr::Bin(BinOp::Ge, Box::new(l), Box::new(r)),
};

OrExpr: Expr = {
  XorExpr,
  <l:OrExpr> "|" <r:XorExpr> => Expr::Bin(BinOp::Or, Box::new(l), Box::new(r))
};

XorExpr: Expr = {
  AndExpr,
  <l:XorExpr> "^" <r:AndExpr> => Expr::Bin(BinOp::Xor, Box::new(l), Box::new(r))
};

AndExpr: Expr = {
  ShiftExpr,
  <l:AndExpr> "&" <r:ShiftExpr> => Expr::Bin(BinOp::And, Box::new(l), Box::new(r))
};

ShiftExpr: Expr = {
  AddExpr,
  <l:ShiftExpr> "<<" <r:AddExpr> => Expr::Bin(BinOp::Lsh, Box::new(l), Box::new(r)),
  <l:ShiftExpr> ">>" <r:AddExpr> => Expr::Bin(BinOp::Rsh, Box::new(l), Box::new(r)),
};

AddExpr: Expr = {
  MulExpr,
  <l:AddExpr> "+" <r:MulExpr> => Expr::Bin(BinOp::Add, Box::new(l), Box::new(r)),
  <l:AddExpr> "-" <r:MulExpr> => Expr::Bin(BinOp::Sub, Box::new(l), Box::new(r)),
};

MulExpr: Expr = {
  CastExpr,
  <l:MulExpr> "*" <r:CastExpr> => Expr::Bin(BinOp::Mul, Box::new(l), Box::new(r)),
  <l:MulExpr> "/" <r:CastExpr> => Expr::Bin(BinOp::Div, Box::new(l), Box::new(r)),
  <l:MulExpr> "%" <r:CastExpr> => Expr::Bin(BinOp::Mod, Box::new(l), Box::new(r)),
};

CastExpr: Expr = {
  PreExpr,
  <e:CastExpr> "as" <t:TypeName> => Expr::Cast(Box::new(e), t),
};

PreExpr: Expr = {
  PostExpr,
  "&" <e:PreExpr> => Expr::Adr(Box::new(e)),
  "*" <e:PreExpr> => Expr::Ind(Box::new(e)),
  "+" <e:PreExpr> => Expr::Un(UnOp::UPlus, Box::new(e)),
  "-" <e:PreExpr> => Expr::Un(UnOp::UMinus, Box::new(e)),
  "~" <e:PreExpr> => Expr::Un(UnOp::Not, Box::new(e)),
  "!" <e:PreExpr> => Expr::LNot(Box::new(e)),
};

PostExpr: Expr = {
  PrimExpr,
  <e:PostExpr> "." <i:Identifier> => Expr::Dot(Box::new(e), i),
  <e:PostExpr> "(" <a:ArgumentListOpt> ")" => Expr::Call(Box::new(e), a),
  <e:PostExpr> "[" <i:Expr> "]" => Expr::Index(Box::new(e), Box::new(i)),
};

ArgumentListOpt: Vec<(RefStr, Expr)> = {
  ArgumentList,
  => vec![],
}

ArgumentList: Vec<(RefStr, Expr)> = {
  <name:Identifier> ":" <expr:Expr> => vec![ (name, expr) ],
  <mut l:ArgumentList> "," <name:Identifier> ":" <expr:Expr> => {
    l.push((name, expr));
    l
  }
};

PrimExpr: Expr = {
  "(" <e:Expr> ")" => e,
  <b:Block> => b,
  <p:Path> => Expr::Path(p),
  "true" => Expr::Bool(true),
  "false" => Expr::Bool(false),
  Integer => Expr::Int(<>),
  Float => Expr::Flt(<>),
  Character => Expr::Char(<>),
  String => Expr::Str(<>),
};

// Modules

TypeDefinition: () = {
  "struct" <name:Identifier>
      <type_params:TypeParameterListOpt> "(" <params:ParameterListOpt> ")" => {
    module.ty_defs.push(TyDef::Struct { name, type_params, params });
  },
  "union" <name:Identifier>
      <type_params:TypeParameterListOpt> "(" <params:ParameterListOpt> ")" => {
    module.ty_defs.push(TyDef::Union { name, type_params, params });
  },
  "enum" <name:Identifier>
      <type_params:TypeParameterListOpt> "(" <variants:VariantListOpt> ")" => {
    module.ty_defs.push(TyDef::Enum { name, type_params, variants });
  },
};

TypeParameterListOpt: Vec<RefStr> = {
                             => vec![],
  "<" <l:IdentifierList> ">" => l,
};

IdentifierList: Vec<RefStr> = {
  Identifier => vec![<>],
  <mut l: IdentifierList> "," <i:Identifier> => {
    l.push(i);
    l
  }
};

VariantListOpt: Vec<(RefStr, Variant)> = {
  VariantList,
  => vec![],
};

VariantList: Vec<(RefStr, Variant)> = {
  <v:Variant> => vec![ (v.0, v.1) ],
  <mut l:VariantList> "," <v:Variant> => {
    l.push((v.0, v.1));
    l
  },
};

Variant: (RefStr, Variant) = {
  Identifier =>
    (<>, Variant::Unit),
  <name:Identifier> "(" <params:ParameterListOpt> ")" =>
    (name, Variant::Struct(params)),
};

ConstDefinition: () = {
  "const" <name:Identifier> ":" <ty:TypeName> "=" <val:Expr> => {
    module.defs.push(Def::Const { name, ty, val });
  }
};

DataDefinition: () = {
  "data" <is_mut:IsMut> <name:Identifier> ":" <ty:TypeName> "=" <init:Expr> => {
    module.defs.push(Def::Data { name, is_mut, ty, init });
  }
};

FunctionDefinition: () = {
  "function" <name:Identifier> <type_params:TypeParameterListOpt>
      "(" <params:ParameterDefinitionListOpt> ")"
      <ret_ty:ReturnType> <body:Block> => {
    module.defs.push(Def::Func { name, type_params, params, ret_ty, body });
  }
};

ParameterDefinitionListOpt: Vec<(RefStr, IsMut, TyRef)> = {
  ParameterDefinitionList,
  => vec![]
};

ParameterDefinitionList: Vec<(RefStr, IsMut, TyRef)> = {
  <is_mut:IsMut> <name:Identifier> ":" <ty:TypeName> => {
    vec![ (name, is_mut, ty) ]
  },
  <mut l:ParameterDefinitionList> "," <is_mut:IsMut> <name:Identifier> ":" <ty:TypeName> => {
    l.push((name, is_mut, ty));
    l
  },
};

ImportDefinition: () = "import" <name:Identifier> => {
  module.deps.insert(name);
};

ExternBlock: () = "extern" "{" Seq<ExternDefinition> "}";

ExternDefinition: () = {
  "function" <name:Identifier> "(" <params:ParameterListOpt> ")" <ret_ty:ReturnType> => {
    module.defs.push(Def::ExternFunc { name, params, ret_ty });
  },
  "data" <is_mut:IsMut> <name:Identifier> ":" <ty:TypeName> => {
    module.defs.push(Def::ExternData { name, is_mut, ty });
  }
};

GlobalDefinition: () = {
  TypeDefinition,
  ConstDefinition,
  DataDefinition,
  FunctionDefinition,
  ImportDefinition,
  ExternBlock,
};

pub Module: () = Seq<GlobalDefinition>;

// Tokens

Identifier: RefStr = r"[_a-zA-Z][_a-zA-Z0-9]*" => RefStr::new(<>);
Integer: usize = r"[0-9]+" => usize::from_str(<>).unwrap();
Float: f64 = r"[0-9]*[.][0-9]+([eE][+-]?[0-9]+)?" => f64::from_str(<>).unwrap();
Character: RefStr = <s:r#"'([^']|\\')*'"#> => RefStr::new(&s[1..s.len()-1]);
String: RefStr = <s:r#""([^"]|\\")*""#> => RefStr::new(&s[1..s.len()-1]);

// Macros

Seq<T>: Vec<T> = <v:(<T>)*> => v;

Comma<T>: Vec<T> = {
  <mut v:(<T> ",")*> <e:T?> => match e {
    None => v,
    Some(e) => {
      v.push(e);
      v
    }
  }
};
